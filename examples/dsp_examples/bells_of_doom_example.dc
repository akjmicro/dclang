"dsp.dc" import

# portamento vars
var myport
var myport_two

: mod_ratio_wobble
  6.37 sine 0.015 * 0.5723 +
;

: trigger_env-1
  # trigger for portamento envelope
  0.2 metro 0.35 *
  # reference array for port and portamento call
  myport 0.04 2.5 port
;

: trigger_env-2
  # trigger for 2nd portamento envelope
  0.5 metro 0.4 *
  # reference array for port and portamento call
  myport_two 0.05 1.3 port
;

: bells_of_doom_example
  inf times
    # FM oscillator:
    230 mod_ratio_wobble 0.9 fm
    # amplitude envelope:
    trigger_env-1 *
    # moving pan signal
    0.23 sine 1 + 0.15 * 0.75 +
    panmix

    # another FM oscillator:
    164 0.7 0.9 fm
    # amplitude envelope:
    trigger_env-2 *
    # another moving pan signal:
    0.1 sine 1 + 0.15 * 0.25 +
    panmix

    # send outputs:
    stereo_out
    # advance sample clock, clear channels
    t+
  again
;

bells_of_doom_example
