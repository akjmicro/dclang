"dsp.dc" import

0.01 0.6  make_port myampport
0.01 0.01 make_port mypchport
var trigval
var curtrig
var lasttrig
var pitch

create choices
  7 , 8 , 9 , 10 , 11 , 12 ,

: randtrig_example
  inf times
    0.02 trigval randtrig         # create a trigger
    curtrig !                     # store it
    curtrig @ lasttrig @ >        # check if it crosses a change
    if                            # does it?
      choices 6 randint + @ 50 *  # calculate a new pitch if so
      pitch !                     # and set it
    endif
    pitch @ mypchport port        # get the pitch, portamento it
    sine                          # assign the portamento-pitch to a sine wave
    curtrig @ 0.7 *               # grab the same trigger signal
    myampport port *              # envelope it
    0.5 panmix                    # center pan
    stereo_out                  # send out
    curtrig @ lasttrig !          # lasttrig = curtrig
    t+                            # update sample clock
  again
;

randtrig_example
