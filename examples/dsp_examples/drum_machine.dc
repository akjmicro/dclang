"dsp.dc" import
"redis_music.dc" import

############################
# Make a 32-step sequencer #
############################
var top_bit       1 31 << top_bit !
############################################################
# A variable to track which bit in the sequence we are on. #
# As the sequence progresses, the bit slot gets smaller    #
# by a factor of two, i.e. 1024, 512, 256, 128, ....       #
############################################################
var seq_bit       top_bit @ seq_bit !

#################
# Set the tempo #
#################
var tempo  :DRUM_TEMPO redis_music.get_x_tempo  tempo !

###############
# Drum Volume #
###############
var drum_vol redis_music.get_drum_vol  drum_vol !

#####################################
# String constants for redis lookup #
#####################################
"op_hihat"   const   :op_hihat
"cl_hihat"   const   :cl_hihat
"snare_drum" const :snare_drum
"bass_drum"  const  :bass_drum

###########################
# Bit patterns for beats! #
###########################
var op_hihat
var cl_hihat
var snare_drum
var bass_drum

: _init_drum_parts
  :op_hihat redis_get
  0 = if
    "0x00020002" :op_hihat redis_set
  endif
  :cl_hihat redis_get
  0 = if
    "0x41413513" :cl_hihat redis_set
  endif
  :snare_drum redis_get
  0 = if
    "0x0808080c" :snare_drum redis_set
  endif
  :bass_drum redis_get
  0 = if
    "0x80818082" :bass_drum redis_set
  endif
;

_init_drum_parts

##################################
# Masking volumes for each sound #
##################################
var   op_hihat_mask
var   cl_hihat_mask
var snare_drum_mask
var  bass_drum_mask

##########################################
# Holding places for the trigger state   #
# Allows us to detect upward signal edge #
##########################################
var curtrig
var lasttrig

########################
# Portamento envelopes #
########################
var op_hihat_port
var cl_hihat_port
var snare_drum_port
var bass_drum_port

##########################################
# Hal Chamberlain filters for the hihats #
##########################################
make_hc_filter  cl_hihat_flt
make_hc_filter  op_hihat_flt

########################################################
# Every sequencer cycle, check redis for a new pattern #
########################################################
: _query_drum_parts
  :op_hihat   redis_get tonum    op_hihat !
  :cl_hihat   redis_get tonum    cl_hihat !
  :snare_drum redis_get tonum  snare_drum !
  :bass_drum  redis_get tonum   bass_drum !
;

_query_drum_parts

#############################################################
# `_next_bit` r-shifts which bit in the hexbeat to inspect   #
# Also, conditionally resets back to the top bit if it      #
# reaches the right most extreme (typically 0, but we could #
# do different meters by resetting earlier)                 #
#############################################################
: _next_bit
  seq_bit @ 1 >> dup
  0 = if
    drop
    top_bit @ seq_bit !
    _query_drum_parts
  else
    seq_bit !
  endif
;

###############################################################
# `_bit_on?` answers the question if the bit we are looking at #
# is "on" (or off) for the instrument line given on the stack #
###############################################################
: _bit_on? { this_var }
  seq_bit @
    dup
      this_var @
    and
  = abs
;

########################################################################
# All `dsp.dc` central words are an infinite loop that return a single #
# sample per iteration.                                                #
########################################################################
: drum_machine
  inf times
    tempo @ metro    curtrig !
    curtrig @ lasttrig @ > if
      :DRUM_TEMPO redis_music.get_x_tempo dup tempo @ <>
      if
        tempo !
      else
        drop
      endif
      redis_music.get_drum_vol dup drum_vol @ <>
      if
        drum_vol !
      else
        drop
      endif
      bass_drum  _bit_on?   bass_drum_mask !
      snare_drum _bit_on?  snare_drum_mask !
      cl_hihat   _bit_on?    cl_hihat_mask !
      op_hihat   _bit_on?    op_hihat_mask !
      _next_bit
    endif
    ###################
    # bass drum sound #
    ###################
    curtrig @ bass_drum_mask @ *
      bass_drum_port 0.001 0.06
    port
      dup 90 * 90 + sine
    *
    drum_vol @ *
    0.5 panmix
    ####################
    # snare drum sound #
    ####################
    curtrig @ snare_drum_mask @ *
      snare_drum_port 0.001 0.06
    port
      noise 0.4 *
        383 sine 0.3 *
      +
    *
    drum_vol @ *
    0.3 panmix
    ######################
    # closed hihat sound #
    ######################
    curtrig @ cl_hihat_mask @ *
      cl_hihat_port 0.001 0.06
    port
      noise 0.2 *
        cl_hihat_flt
          3610 0.6
      hc_bandpass
    *
    drum_vol @ *
    0.7 panmix
    ####################
    # open hihat sound #
    ####################
    curtrig @ op_hihat_mask @ *
      op_hihat_port 0.001 0.18
    port
      noise 0.3 *
        op_hihat_flt
          3800 0.7
      hc_bandpass
    *
    drum_vol @ *
    0.7 panmix
    ############################
    # output & variable update #
    ############################
    stereo_out
    curtrig @ lasttrig !
    t+
  again
;

###########
# Run it! #
###########

drum_machine
