"midi.dc" import
"redis_music.dc" import

17 midi_edo !

: generate_interval
  / log2 midi_edo @ * round
;

9 8 generate_interval const STEP
6 5 generate_interval const MIN3
6 5 generate_interval const THIRD
3 2 generate_interval const FIFTH
9 5 generate_interval const SEV

create arpeggio 0 , STEP , THIRD , FIFTH , SEV ,
create attacks 96 , 32 , 32 , 32 , 32 ,
create modulations FIFTH , MIN3 , FIFTH , MIN3 ,
var stopgap
attacks arpeggio - const ARPEGGIO_SIZE
ARPEGGIO_SIZE 2 * const MEASURE
stopgap modulations - const MODUSIZE
var step
0 step !
var modulations_pointer
0 modulations_pointer !

: _advance_step
  step @ 1 + step !
;

: _get_modulations_pointer
  modulations_pointer @ dup
  MODUSIZE =
  if
    drop
    0
  endif
  dup 1 + modulations_pointer !
;

: _update_arpeggio
  svpush
  ARPEGGIO_SIZE times
    arpeggio i + @
    0 svpick +   # interval on stack is added here
    midi_edo @ %
    arpeggio i + !
  again
  svdrop
  # lower first note (root) an octave
  arpeggio @ midi_edo @ -
  arpeggio !
  arpeggio ARPEGGIO_SIZE sortnums
;

: _do_transposition
  step @ MEASURE %
  0 =
  if
    modulations
    _get_modulations_pointer
    + @
    _update_arpeggio
  endif
;

: _get_edo_note
  step @
  ARPEGGIO_SIZE %
  attacks + @
  step @
  ARPEGGIO_SIZE %
  arpeggio + @
;

: main
  block_sigint
        _get_edo_note dup svpush 0
  edo_degree_to_midi
  redis_music.get_on_gate sleep
        0 svpop 0
  edo_degree_to_midi
  redis_music.get_off_gate sleep
  unblock_sigint
  _advance_step
  _do_transposition
  main
;

main

8192 0 0xE0 send_midi_reverse
