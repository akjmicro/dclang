# This illustrates using decimal expansions of a fraction division in any base
# being used to make music via MIDI
#
# First, make sure you have a running redis server on your machine
#
# Then, to run, do `redis-cli` in a shell, making sure you set the following vars:
#     tempo, gate, base, div, mode, transp
# (they should all be integers)
#
# Then, in another shell, open this script, your command should be:
# dclang midi_from_redis.dc

"math.dc" import
"midi.dc" import
"redis_midi.dc" import

# our EDO var
17 midi_edo !

create scale -6 , -3 , 1 , 4 , 8 , 11 , 14 , 18 , 21 , 25 , 28 ,
var dummy
var scale_size
dummy scale -
scale_size !

var gen_struct 3 allot

: _get_next_note
    redis_midi.get_base gen_struct _set_base
    redis_midi.get_div  gen_struct _set_div
    gen_struct gen_digit_expansion
      redis_midi.get_mode
    +
      scale_size @
    absmod
      scale
    +
    @
      redis_midi.get_transpose
    +
;

: _midi_from_redis
  block_sigint
  ###
    100
      _get_next_note
      dup svpush
      0
  edo_degree_to_midi
  ###
    redis_midi.get_on_gate
  sleep
        0 svpop 0
  edo_degree_to_midi
    redis_midi.get_off_gate
  sleep
  unblock_sigint
  _midi_from_redis
;

: midi_from_redis
  "You are now entering an endless loop, hit CTRL-C to stop..." print cr
  "Notice that you won't be able to stop until a note off event hits." print cr
  "In this way, the notes end cleanly!" print cr
  _midi_from_redis
;

midi_from_redis

panic
