####################################################################
# Still in early stages, but the idea is to implement a Redis API  #
#                                                                  #
# 2022-11-05: updated with a refactor to not use string templates, #
#             but rather, append elements continuously to the r/w  #
#             buffer. The first substring added is a 'header'.     #
#                                                                  #
# 2025-03-24: change in API for connecting. You can set envvars    #
#             for REDIS_HOST, REDIS_PORT, and REDISCLI_AUTH (pw).  #
#             If unset in the env, defaults will be used, which    #
#             are 'localhost' and port 6379. `redis_connect` will  #
#             give textual feedback about the connection being     #
#             made, too.                                           #
#                                                                  #
# TODO: There is a limit now for buffer size, and thus the code is #
#       usuable for 90% of cases where there are small keys/vals,  #
#       but it not yet particularly robust for dynamically and     #
#       arbitrary message length. (Buffer size now is 1024 bytes.) #
####################################################################

"string.dc" import

# constants
1024 const REDIS_BUFSIZE
REDIS_BUFSIZE mkbuf const REDIS_BUFFER

# token_save space for `strtok` variable; socket pointer variable
var token_save
var redis_socket -1 redis_socket !

# for file-based reading
var use_file  1 use_file !
"r"          const :read
"/tmp/keys/" const :tmpdir
var tmpbuf
32 mkbuf tmpbuf !

# various redis function API string 'headers'
"\r\n" const :RN
"\r\n$" const :RN$
"*2\r\n$4\r\nAUTH\r\n$" const :REDIS_AUTH_HEADER
"*3\r\n$3\r\nSET\r\n$"  const :REDIS_SET_HEADER
"*2\r\n$3\r\nGET\r\n$"  const :REDIS_GET_HEADER
"*2\r\n$4\r\nKEYS\r\n$" const :REDIS_SEARCH_HEADER
"*2\r\n$3\r\nDEL\r\n$"  const :REDIS_DELETE_HEADER

######################################
# buffer and tcp communication words #
######################################

: _zero_redis_buffer
  REDIS_BUFFER 0 REDIS_BUFSIZE memset
;

: _write_redis_buffer
    redis_socket @
      REDIS_BUFFER
        #_get_msglen
        dup strlen
    write
  drop
;

: _read_redis_buffer
    _zero_redis_buffer
      redis_socket @ swap
        REDIS_BUFSIZE
    read
  drop
  REDIS_BUFFER
;

: _redis_write_read
  _write_redis_buffer
  _read_redis_buffer
;

: _redis_auth
  "REDISCLI_AUTH" envget dup
  0 = if
    drop
    return  # If no password is set, do nothing
  endif
  svpush
  _zero_redis_buffer
  :REDIS_AUTH_HEADER str+
  0 svpick strlen tostr str+
  :RN str+
  svpop str+
  :RN str+
  drop
  _redis_write_read
  # TODO: Check authentication response
  drop
;

#########################
# MAIN PUBLIC API WORDS #
#########################

: redis_connect
  "REDIS_HOST" envget dup
  0 = if
    drop
    "localhost"
  endif
  "REDIS_PORT" envget dup
  0 = if
    drop
    "6379"
  endif
  tonum
  "REDIS_SILENT_CONNECT" envget
  0 = if
    2dup swap
    "Connecting to redis using host: " print print
    " and port " print . cr
  endif
  tcpconnect redis_socket !
  _redis_auth
;

: redis_set
  depth 2 <
  if
    "redis_set needs <val_str> <key_str> on the stack!" print cr
    return
  endif
  swap svpush svpush
  _zero_redis_buffer
  :REDIS_SET_HEADER str+
  0 svpick strlen tostr str+
  :RN str+
  svpop str+
  :RN$ str+
  0 svpick strlen tostr str+
  :RN str+
  svpop str+
  :RN str+
  drop
  _redis_write_read
  # TODO: actually check status
  drop
;


: _file_get { key }
  tmpbuf @ 0 32 memset
  :tmpdir str+ key str+ drop  # done building path string
  tmpbuf @ :read fopen
  dup -1 = if
    drop "0" return
  endif
  dup freadall drop
  # cleanup
  swap fclose
  key free
;

: _redis_get
  svpush
  _zero_redis_buffer
  :REDIS_GET_HEADER str+
  0 svpick strlen tostr str+
  :RN str+
  svpop str+
  :RN str+
  drop
  # for now, ignore the status string and length which represents
  # the first line of the response. Our key's value sits in the
  # second line.
  _redis_write_read :RN token_save strtok drop
  null :RN token_save strtok
;

: redis_get # ( key_str )
  depth 1 <
  if
    "redis_get needs <key_str> on the stack!" print cr
    return
  endif
  use_file @ if
    _file_get
  else
    _redis_get
  endif
;

: redis_keys
  depth 1 <
  if
    "redis_keys needs <search_str> on the stack!" print cr
    "You can try using something like:" print cr cr
    "\"*\" redis_keys print cr" print cr
    return
  endif
  svpush
  _zero_redis_buffer
  :REDIS_SEARCH_HEADER str+
  0 svpick strlen tostr str+
  :RN str+
  svpop str+
  :RN str+
  drop
  _redis_write_read
;

: redis_del
  depth 1 <
  if
    "redis_del needs <key_to_delete> on the stack!" print cr
    return
  endif
  svpush
  _zero_redis_buffer
  :REDIS_DELETE_HEADER str+
  0 svpick strlen tostr str+
  :RN str+
  svpop str+
  :RN str+
  drop
  _redis_write_read
  # TODO: actually check status
  drop
;
