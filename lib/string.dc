"deprecated_stack.dc" import

# Set up a small _private_ buffer for doing string slices.
# Used by `strslice` below
1024 const SLICEBUFSIZE
var _slicebuf
SLICEBUFSIZE mkbuf _slicebuf !

# Some error messages we re-use
"strslice: stack underflow -- must have <straddr> <start_idx> <end_idx> on the stack!"
const :STRSLICE_UNDERFLOW_ERROR

"strslice: error -- cannot slice a string with the start_idx > end_idx!"
const :STRSLICE_NOT_LTE_ERROR

"strslice: error -- cannot slice a string with negative indices!"
const :STRSLICE_NEG_ERROR

"str_between: stack underflow -- must have <straddr> <bottom_str> <top_str> on the stack!"
const :STRBETWEEN_UNDERFLOW_ERROR

"strreplace needs <haystack> <needle> <replacement_needle> on the stack"
const :STRREPLACE_UNDERFLOW_ERROR

"str_split needs <str_to_split> <split_char> on the stack"
const :STRSPLIT_UNDERFLOW_ERROR

: strdup
  # copies a string; leaves the new string's
  # beginning address on the stack
  dup strlen 1 +
  swap over mkbuf
  swap 2 pick mempcpy
  swap -
;

: str+
  # N.B. the intention in using this is to start the "chain"
  # of added strings with a user-initiated empty buffer
  # of appropriate size.
  dup strlen mempcpy
;

: strslice
  # Need the following on the stack to use (TOS is rightmost):
  # (string_addr start_idx end_idx)
  ####################
  # Validation steps #
  ####################
  # First check the stack depth. We need at least 3 parms:
  depth 3 <
  if
    :STRSLICE_UNDERFLOW_ERROR print cr
    clear return
  endif
  # Now, check that start index isn't bigger than end index:
  2dup <= not
  if
    :STRSLICE_NOT_LTE_ERROR print cr
    clear return
  endif
  # Finally, check that both #s aren't negative:
  2dup
  0 <
  if
    :STRSLICE_NEG_ERROR print cr
    clear return
  endif
  0 <
  if
    :STRSLICE_NEG_ERROR print cr
    clear return
  endif
  ########################
  # END VALIDATION STEPS #
  ########################
  # Clear the `_strslice` buffer:
  _slicebuf @ 0 SLICEBUFSIZE memset drop
  # Now, actually deal with the user input:
  over -
  svpush +
  svpush
  _slicebuf @ svpop svpop mempcpy
  drop
  _slicebuf @
;

: str_between
  depth 3 <
  if
    :STRBETWEEN_UNDERFLOW_ERROR print cr
    clear return
  endif
  # ( str_pnt bottom top ) .s
  2 pick           # ( str_pnt bottom top str_pnt )
  str< not svpush  # ( str_pnt bottom ) ( not_outside )
  str< not         # ( not_outside ) ( not_outside )
  svpop and
;

: _incr_saved_counter
  svpop 1 + svpush
;

: _substr_count_helper
  # ( substr str ) ( 0 ) -> ( cnt ) ( )
  over strfind dup
  if
    _incr_saved_counter
    over strlen +
    _substr_count_helper
  else
    2drop svpop
  endif
;

: substr_count
  # ( str substr ) -> # ( substr str ) ( 0 )
  0 svpush swap
  _substr_count_helper
;

: _setup_replace_buffer
  # ( str findstr replacestr times )
  rot rot
  strlen swap strlen - *
  swap strlen +
  mkbuf
;

: _strreplace_setup
  # ( str find repl )
  2 pick 2 pick substr_count
  # ( str find repl count )
  2over 2over _setup_replace_buffer
  # ( str find repl count buffer )
  nip
  # ( str find repl buffer )
  swap
  # ( str find buffer repl )
  svpush
  # ( str find buffer ) ( repl )
  -rot
  # ( buffer str find ) ( repl )
  svpop
  # ( buffer str find repl )
;

: _strreplace_helper
  2over 2over drop strfind dup
  # ( buffer str find repl buffer found found ) ( buffer )
  if
  # ( buffer str find repl buffer found ) ( buffer )
    dup svpush
    # ( buffer str find repl buffer found ) ( found buffer )
    4 pick tuck -
    # ( buffer str find repl buffer str sz ) ( found buffer )
    mempcpy over dup strlen mempcpy
    # ( buffer str find repl newbufrepl ) ( found buffer )
    svpop 3 pick strlen +
    # ( buffer str find repl newbufrepl found+findlen ) ( buffer )
    2rot 2drop 2swap
    # ( newbufrepl found+findlen find repl ) ( buffer )
    _strreplace_helper
  else
  # ( buffer str find repl buffer found ) ( buffer )
    2drop 2drop dup strlen
  # ( buffer str sz )
    mempcpy drop svpop
  # ( buffer )
  endif
;

: strreplace
  depth 3 <
  if
    :STRREPLACE_UNDERFLOW_ERROR print cr
    return
  endif
  _strreplace_setup
  # ( buffer str find repl )
  3 pick svpush
  # ( buffer str find repl ) ( buffer )
  _strreplace_helper
;

: _get_next_num_from_line
  # need a strtok ptr and a linestr_ptr
  # ( ss lp ) ()
  svpush svpush
  # ( ) ( lp ss )
  # return early if the line point is itself null
  1 svpick @ null =
  if
    svdrop svdrop
    0 return
  endif
  0 svpick @ null =
  if
    1 svpick @ " \n" 0 svpick strtok
    svdrop svdrop
  else
    null " \n" 0 svpick strtok
    svdrop svdrop
  endif
;

: next_num_from_file
  # an iterator over a file with just whitespace separated numbers
  # start with <strtok_space> <linestr_space> <open_file_pointer>
  # on the data stack
  # ( ss lp fp ) ()
  svpush svpush svpush
  # () ( fp lp ss )
  0 svpick 1 svpick _get_next_num_from_line
  dup 0 =
  if
    drop
    0 0 svpick !
    2 svpick @ freadline
    -1 =    # -1 == EOF (end-of-file)
    if
      drop
      0 0 svpick !
      0 1 svpick !
      svdrop svdrop svdrop
      0 return
    else
      1 svpick !
      0 svpick 1 svpick _get_next_num_from_line
      svdrop svdrop svdrop
      return
    endif
  else
    svdrop svdrop svdrop
    return
  endif
;

create hexchars 48 , 49 , 50 , 51 , 52 , 53 , 54 , 55 , 56 , 57 ,
                97 , 98 , 99 , 100 , 101 , 102 ,

: uuid
  37 mkbuf dup svpush
  8 times
      rand 16 * floor hexchars + @
        1
    memset
    1 +
  again
  45 1 memset
  1 +
  4 times
      rand 16 * floor hexchars + @
        1
    memset
    1 +
  again
  45 1 memset
  1 +
  4 times
      rand 16 * floor hexchars + @
        1
    memset
    1 +
  again
  45 1 memset
  1 +
  4 times
      rand 16 * floor hexchars + @
        1
    memset
    1 +
  again
  45 1 memset
  1 +
  12 times
      rand 16 * floor hexchars + @
        1
    memset
    1 +
  again
  0 1 memset drop
  svpop
;

: hex8token
  9 mkbuf dup svpush
  8 times
      rand 16 * floor hexchars + @
        1
    memset
    1 +
  again
  0 1 memset drop
  svpop
;

#######################################################
# New word: `str_split`                               #
# Will return a linked list with all found substrings #
# within `in_str` that are separated by `split_token` #
#######################################################

var _split_list
var _strtok_state

: _str_split_real { in_str split_token }
  lmake _split_list !
  in_str split_token _strtok_state strtok dup
  0 = if
    _split_list @
    return
  else
    _split_list @ swap lpush
    inf times
      null split_token _strtok_state strtok dup
      0 =
    if
      drop
      _split_list @
      exittimes
    else
      _split_list @ swap lpush
      again
    endif
  endif
;

: str_split
  depth 2 <
  if
    :STRSPLIT_UNDERFLOW_ERROR print cr
    return
  endif
  _str_split_real
;

: chop_head { haystack needle }
  # `chop_head` will cut the front off of a `haystack` string,
  # all the way up to the end of the match of `needle_str`.
  # Example:
  # "This is my special sentence, full of sauce" ", " chop_head
  # will return:
  # "full of sauce"
  haystack needle strfind needle strlen +  # ( newstr )
  dup        # ( newstr newstr )
  strlen 1 + # ( newstr newlen )
  mkbuf      # ( newstr result )
  dup svpush # ( newstr result )   -> ( result )
  swap       # ( result newstr )      ( result )
  dup strlen # ( result newstr size ) ( result )
  mempcpy    # ( )                    ( result )
  svpop      # ( result )
;

: chop_tail { haystack needle }
  # `chop_tail` will cut the back off of a `haystack` string,
  # starting from the match of `needle_str`.
  # Example:
  # "This is my special sentence, full of sauce" ", " chop_tail
  # will return:
  # "This is my special sentence"
  haystack dup needle strfind "" strreplace
;

: chop_between { haystack needle1 needle2 }
  # Combines `chop_head` and `chop_tail` to return a middle string
  # Example:
  # "This is my special sentence, full of sauce" ", " " of" chop_between
  # will return:
  # "full"
  haystack needle1 chop_head needle2 chop_tail
;

"This is my special sentence, full of sauce" ", " " of" chop_between
"full" str= assert
