"redis.dc" import

# make the redis connection
redis_connect

var remainder 1 remainder !
var _tok1
var _tok2
var _tok3
var _tok4
var _tok5
var _tok6
var _tok7
# some symbols
"\n" const :NL
"tempo" const :TEMPO
"gate" const :GATE
"mode" const :MODE
"transp" const :TRANSPOSE
# for fraction/remainder based "randomess":
"base" const :BASE
"div" const :DIV
# for chaos-based randomness:
"chaos" const :CHAOS

#############################################################
# Helpers for algorithmic MIDI processes...                 #
# At the moment, there are two: fraction-based sequences    #
# that use digit expansion to a certain base, and a "chaos" #
# factor concept.                                           #
#############################################################

: get_remainder remainder @ dup 0 = if drop 1 endif ;
: set_remainder remainder ! ;

: get_div
  :DIV redis_get :NL _tok5 strtok drop
  0 :NL _tok5 strtok tonum round
  dup 0 = if drop 1 endif
;

: get_base
  :BASE redis_get :NL _tok6 strtok drop
  0 :NL _tok6 strtok tonum round
;

: get_chaos
  :CHAOS redis_get :NL _tok7 strtok drop
  0 :NL _tok7 strtok tonum round
;

###########################################
# private words for dealing with duration #
###########################################

: _get_tempo
  :TEMPO redis_get :NL _tok1 strtok drop
  0 :NL _tok1 strtok tonum 60 swap /
;

: _get_gate
  :GATE redis_get :NL _tok2 strtok drop
  0 :NL _tok2 strtok tonum 100 /
;

###############################################################
# public API "duration" words to be used in calling code.     #
# The idea is to just call on/off durations directly          #
# and let the tempo and gate do the work in the background... #
###############################################################

: get_on_gate
  _get_tempo
  _get_gate *
  0.25 *
;

: get_off_gate
  _get_tempo
  1 _get_gate - *
  0.25 *
;

########################
# mode & transposition #
########################

: get_mode
  :MODE redis_get :NL _tok3 strtok drop
  0 :NL _tok3 strtok tonum round
;

: get_transpose
  :TRANSPOSE redis_get :NL _tok4 strtok drop
  0 :NL _tok4 strtok tonum round
;
