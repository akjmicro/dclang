"string.dc" import

"PM_IN"  const :PM_IN
"PM_OUT" const :PM_OUT

# Adding the ability to have multiple instances running with different
# MIDI channels. Code will later subtract 1 to get 0-based indices.

var midi_channel

: populate_midi_channel
  "MIDI_CHANNEL" envget dup 0 = if drop 1 else tonum endif midi_channel !
;

populate_midi_channel

: make_channel_string { prefix }  # used by e.g. `music_keyval.dc` and `music.dc` libs
  32 mkbuf dup svpush
  prefix str+
  midi_channel @ tostr str+
  drop svpop
;

# END adding MIDI CHANNEL strings


# These will be used again (unused ATM) once MIDI read functionality
# is re-implemented via `portmidi`
var midi_ctl_slots 128 allot
var midi_channel_last_note 16 allot

: midi_shutdown
  _pm_close
  _pm_terminate
;

: check_mididev
  # Set to stderr printing
  seterr
  # First, list MIDI devices found:
  cr "Here are your available MIDI devices:" print cr
  _pm_list cr
  # Try to set output device
  :PM_OUT envget dup
  0 =
  if
    :PM_OUT print " is not set in your environment!" print cr
    "MIDI output functionality will not work." print cr
    "If you need this, please exit from dclang, set that value, and try again!" print cr
    drop
  else
      tonum
        "Opening output device number: " print dup . cr
    _pm_open_out
  endif
  # Try to set input device
  :PM_IN envget dup
  0 =
  if
    :PM_IN print " is not set in your environment!" print cr
    "MIDI input functionality will not work." print cr
    "If you need this, please exit from dclang, set that value, and try again!" print cr
    drop
  else
      tonum
        "Opening input device number: " print dup . cr
    _pm_open_in
  endif
  # Reset to stdout printing
  setout
;

check_mididev

# update midictl state by polling for incoming messages
: refresh_midictl
  _pm_read
  0xff and 0xb0 =
  if
    midi_ctl_slots + !
  else
    2drop
  endif
;

# This is the main API for grabbing a controller variable
# ( ctrlnum -- sig )
: midictl midi_ctl_slots + @ ;

# basic connection words
: send_midi _pm_ws ;

: send_midi_reverse _pm_wsr ;

# useful for when things go nuts!
: panic
  128 0 1
  for
      0x80
        midi_channel @ 1 -
      +
        i
          0
    send_midi
    0.01 sleep
  next
  8192
    0
      0xE0
        midi_channel @ 1 -
      +
  send_midi_reverse
;

: _bend_to_MSB_LSB_bytes
  # a helper function. Given a top-of-stack 14-byte integer MIDI
  # pitch-bend amount, replace the top-of-stack with two bytes,
  # the top-of-stack being the LSB (least significant byte) and the next
  # being the most significant byte
  dup 7 >>
  swap 0x7f and
;
